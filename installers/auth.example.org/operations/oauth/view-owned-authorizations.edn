{:install
 {:juxt.site/subject-uri "https://auth.example.org/_site/subjects/system"
  :juxt.site/operation-uri "https://auth.example.org/_site/operations/create-operation"
  :juxt.site/input
  {:xt/id "{{$id}}"
   :juxt.site/do-operation-tx-fn "https://auth.example.org/_site/do-operation"

   :juxt.site/state
   {:juxt.site.sci/program
    #juxt.pprint
    (let [{client-id "client_id"
           redirect-uri "redirect_uri"
           scope "scope"
           :as query}
          (ring.util.codec/form-decode (:ring.request/query *ctx*))

          access-ranges-requested (when scope (clojure.string/split scope (re-pattern "\\s+")))

          ;; Query for scope details in database
          access-ranges-details
          (when access-ranges-requested
            (into []
                  (map first
                       (xt/q '{:find [(pull e [*])]
                               :where [[e :juxt.site/type "https://meta.juxt.site/types/scope"]
                                       [e :xt/id id]
                                       [(contains? ids id)]]
                               :in [ids]
                               }
                             (set access-ranges-requested)))))

          applications (juxt.site/lookup-applications client-id)
          application (first applications)
          acceptable-redirect-uris (:juxt.site/redirect-uris application)

          strip-ns (fn [m] (into {} (map (fn [[k v]] [(keyword (name k)) v]) m)))
          ]

      (merge
       {:query query
        :client-id client-id
        :redirect-uri redirect-uri
        :valid-redirect-uri? (contains? (set acceptable-redirect-uris) redirect-uri)
        }
       (when access-ranges-requested
         {:access-ranges-requested access-ranges-requested
          :access-ranges-details (mapv strip-ns access-ranges-details)})
       (when application
         {:application (strip-ns application)})))}

   :juxt.site/rules
   [
    ;; Anyone who is logged in can authorize
    [(allowed? subject operation resource permission)
     [permission :xt/id]
     [subject :xt/id]]]}}}
