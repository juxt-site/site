{:install
 {:juxt.site/subject-uri "https://auth.example.org/_site/subjects/system"
  :juxt.site/operation-uri "https://auth.example.org/_site/operations/create-operation"
  :juxt.site/input
  {:xt/id "{{$id}}"
   :juxt.site/do-operation-tx-fn "https://auth.example.org/_site/do-operation"

   :juxt.site/state
   {:juxt.site.sci/program
    #juxt.pprint
    (->>
     (xt/q
      '{:find [(pull e [*])]
        :where [[e :juxt.site/type "https://data.example.org/types/order"]]})
     (map first)
     (group-by :status)
     (reduce
      (fn [acc [key groups]]
        (assoc acc key (count groups)))
      {}))}

   :juxt.site/scope
   #{"https://auth.example.org/scopes/petstore/read"}

   :juxt.site/rules
   [[(allowed? subject operation resource permission)
     [subject :juxt.site/user user]
     [permission :juxt.site/role role]
     [role :juxt.site/type "https://meta.juxt.site/types/role"]
     [role-assignment :juxt.site/type "https://meta.juxt.site/types/role-assignment"]
     [role-assignment :juxt.site/role role]
     [role-assignment :juxt.site/user user]]

    [(allowed? subject operation resource permission)
     [subject :juxt.site/application app]
     [permission :juxt.site/role role]
     [role :juxt.site/type "https://meta.juxt.site/types/role"]
     [role-assignment :juxt.site/type "https://meta.juxt.site/types/role-assignment"]
     [role-assignment :juxt.site/role role]
     [role-assignment :juxt.site/application app]]]}}}
