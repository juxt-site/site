{:install
 {:juxt.site/subject-uri "https://auth.example.org/_site/subjects/system"
  :juxt.site/operation-uri "https://auth.example.org/_site/operations/create-operation"
  :juxt.site/input
  {:xt/id "{{$id}}"

   :juxt.site.malli/input-schema
   [:map
    [:xt/id [:re "https://data.example.org/hospital-demo/patients/.*"]]
    [:name [:string {:min 3}]]]

   :juxt.site/prepare
   {:juxt.site.sci/program
    #juxt.pprint
    (let [content-type (-> *ctx*
                           :juxt.site/received-representation
                           :juxt.http/content-type)
          body (-> *ctx*
                   :juxt.site/received-representation
                   :juxt.http/body)]
      (case content-type
        "application/edn"
        (let [input
              (some->
               body
               (String.)
               clojure.edn/read-string
               juxt.site.malli/validate-input
               (assoc
                :juxt.site/type "https://data.example.org/types/patient"
                :juxt.site/protection-spaces #{"https://auth.example.org/protection-spaces/bearer"}
                :juxt.site/methods
                {:get {:juxt.site/operation "https://data.example.org/hospital-demo/_site/operations/get-patient"}
                 :options {}}))]
          (assoc input
                 :juxt.http/content-type "application/json"
                 :juxt.http/content (jsonista.core/write-value-as-string {"name" (:name input)})))))}

   :juxt.site/transact
   {:juxt.site.sci/program
    #juxt.pprint
    [[:xtdb.api/put *prepare*]]}

   :juxt.site/rules
   [
    [(allowed? subject operation resource permission)
     [permission :juxt.site/subject subject]]]}}}
